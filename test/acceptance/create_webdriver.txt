*Setting*
Resource  resource.txt
Library  Collections

*Test Cases*
Create Webdriver Creates Functioning WebDriver
  [Documentation]  LOG 2:1 INFO REGEXP: Creating an instance of the \\w+ WebDriver LOG 2:3 DEBUG REGEXP: Created \\w+ WebDriver instance with session id (\\w|-)+
  [Setup]  Set Driver Name
  Create Webdriver  ${DRIVER NAME}  createwebdriver
  Go To  ${FRONT PAGE}
  Page Should Contain  needle
  [Teardown]  Close Browser

Create Webdriver With Bad Driver Name
  Run Keyword And Expect Error  'Fireox' is not a valid WebDriver name  Create Webdriver  Fireox

Create Webdriver With Duplicate Arguments
  ${kwargs}=  Create Dictionary  arg  1
  Run Keyword And Expect Error  Got multiple values for argument 'arg'.  Create Webdriver  Firefox  kwargs=${kwargs}  arg=2

Create Webdriver With Bad Keyword Argument Dictionary
  Run Keyword And Expect Error  kwargs must be a dictionary.  Create Webdriver  Firefox  kwargs={'spam': 'eggs'}

*Keywords*
Set Driver Name
  ${drivers}=  Create Dictionary  firefox  Firefox  ie  Ie  internetexplorer  Ie  googlechrome  Chrome  gc  Chrome  opera  Opera  phantomjs  PhantomJS  safari  Safari
  ${name}=  Get From Dictionary  ${drivers}  ${browser.lower().replace(' ', '')}
  Set Test Variable  ${DRIVER NAME}  ${name}
