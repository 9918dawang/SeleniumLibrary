*Setting*
Suite Teardown  Close All Browsers
Resource  resource.txt
Library  Collections

*Test Cases*
Create Webdriver Creates Functioning WebDriver
  [Documentation]  LOG 1:1 INFO Creating an instance of the Firefox WebDriver LOG 1:3 DEBUG REGEXP: Created Firefox WebDriver instance with session id (\\w|-)+
  ${index}=  Create Webdriver  Firefox  createwebdriver
  Go To  ${FRONT PAGE}
  Page Should Contain  needle
  ${index as int}=  Convert To Integer  ${index}
  Should Be Equal  ${index}  ${index as int}
  Switch Browser  createwebdriver
  [Teardown]  Close Browser

Create Webdriver With Keyword Argument
  ${proxy}=  Create Bad Proxy
  Create Webdriver  Firefox  proxy=${proxy}
  Go To  ${FRONT PAGE}
  Page Should Contain  The proxy server is refusing connections
  [Teardown]  Close Browser

Create Webdriver With Keyword Argument Dictionary
  ${proxy}=  Create Bad Proxy
  ${kwargs}=  Create Dictionary  proxy  ${proxy}
  Create Webdriver  Firefox  kwargs=${kwargs}
  Go To  ${FRONT PAGE}
  Page Should Contain  The proxy server is refusing connections
  [Teardown]  Close Browser

Create Webdriver With Bad Driver Name
  Run Keyword And Expect Error  'Fireox' is not a valid WebDriver name  Create Webdriver  Fireox

Create Webdriver With Duplicate Arguments
  ${kwargs}=  Create Dictionary  arg  1
  Run Keyword And Expect Error  Got multiple values for argument 'arg'.  Create Webdriver  Firefox  kwargs=${kwargs}  arg=2

Create Webdriver With Bad Keyword Argument Dictionary
  Run Keyword And Expect Error  kwargs must be a dictionary.  Create Webdriver  Firefox  kwargs={'spam': 'eggs'}

*Keywords*
Create Bad Proxy
  ${proxy}=  Evaluate  sys.modules['selenium.webdriver'].Proxy()  sys, selenium.webdriver
  ${proxy.http_proxy}=  Set Variable  localhost:7777
  [Return]  ${proxy}
